import groovy.json.JsonSlurper
import groovy.json.JsonOutput

plugins {
	id 'fabric-loom' version '0.6-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation 'com.github.Draylar:structurized:1.2.0-1.16.5'
	include 'com.github.Draylar:structurized:1.2.0-1.16.5'

	modImplementation "com.github.Draylar.omega-config:omega-config-base:${project.config_version}"
	include "com.github.Draylar.omega-config:omega-config-base:${project.config_version}:min"

	modImplementation "com.github.Draylar.omega-config:omega-config-gui:${project.config_version}"
	include "com.github.Draylar.omega-config:omega-config-gui:${project.config_version}:min"

	modImplementation include("dev.monarkhes:myron:1.5.1")

}
repositories {

	maven { url 'https://jitpack.io' }
	maven {
		name = "OnyxStudios"
		url = "https://maven.abusedmaster.xyz"
	}
	maven { url = "https://hephaestus.dev/release" }
}
processResources {
	doLast {

		def jsonMinifyStart = System.currentTimeMillis()
		def jsonMinified = 0
		def jsonBytesSaved = 0

		fileTree(dir: outputs.files.asPath, include: '**/*.json').each {

			File file = it
			jsonMinified++
			def oldLength = file.length()
			file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
			jsonBytesSaved += oldLength - file.length()
		}

		println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE"
}

